/*@

predicate atomic_space_(list<int> ns, predicate() inv;);

lemma void create_atomic_space_(list<int> ns, predicate() inv)
  requires inv();
  ensures atomic_space_(ns, inv);
{ assume(false); }

predicate atomic_spaces(list<pair<list<int>, predicate()> > spaces);

lemma void open_atomic_space(list<int> ns, predicate() inv)
  requires [?f]atomic_space_(ns, inv) &*& atomic_spaces(?spaces) &*& !mem(pair(ns, inv), spaces);
  ensures [f]atomic_space_(ns, inv) &*& atomic_spaces(cons(pair(ns, inv), spaces)) &*& inv();
{ assume(false); }

lemma void close_atomic_space(list<int> ns, predicate() inv)
  requires inv() &*& atomic_spaces(?spaces);
  ensures atomic_spaces(remove(pair(ns, inv), spaces));
{ assume(false); }

fixpoint boolean is_prefix_of<t>(list<t> xs, list<t> ys) {
  return take(length(xs), ys) == xs;
}

@*/

/*@

typedef lemma void AtomicLong_get_op(AtomicLong l, predicate() P, predicate(long) Q)();
  requires [?f]l.state(?value) &*& P();
  ensures [f]l.state(value) &*& Q(value);

typedef lemma void AtomicLong_get_ghost_op(AtomicLong l, predicate() pre, predicate(long) post)(AtomicLong_get_op *op);
  requires atomic_spaces({}) &*& is_AtomicLong_get_op(op, l, ?P, ?Q) &*& P() &*& pre();
  ensures atomic_spaces({}) &*& is_AtomicLong_get_op(op, l, P, Q) &*& Q(?value) &*& post(value);

typedef lemma void AtomicLong_getAndIncrement_op(AtomicLong l, predicate() P, predicate(long) Q)();
  requires l.state(?value) &*& P();
  ensures l.state(value + 1) &*& Q(value); // No overflow! Sound only if no other mutators are offered!

typedef lemma void AtomicLong_getAndIncrement_ghost_op(AtomicLong l, predicate() pre, predicate(long) post)(AtomicLong_getAndIncrement_op *op);
  requires atomic_spaces({}) &*& is_AtomicLong_getAndIncrement_op(op, l, ?P, ?Q) &*& P() &*& pre();
  ensures atomic_spaces({}) &*& is_AtomicLong_getAndIncrement_op(op, l, P, Q) &*& Q(?value) &*& post(value);

@*/

final class AtomicLong {

  //@ predicate state(long value);

  public AtomicLong()
  //@ requires true;
  //@ ensures state(0);
  { throw new RuntimeException(); }

  public long get()
  //@ requires is_AtomicLong_get_ghost_op(?ghop, this, ?pre, ?post) &*& pre();
  //@ ensures post(result);
  { throw new RuntimeException(); }

  public long getAndIncrement()
  //@ requires is_AtomicLong_getAndIncrement_ghost_op(?ghop, this, ?pre, ?post) &*& pre();
  //@ ensures post(result);
  { throw new RuntimeException(); }

}
