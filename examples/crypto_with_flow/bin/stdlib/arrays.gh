#ifndef ARRAYS_H
#define ARRAYS_H

#include "listex.gh"

fixpoint bool int_within_limits(int x) { return INT_MIN <= x && x <= INT_MAX; }

lemma void ints_limits(int *pxs);
    requires [?f]ints(pxs, ?N, ?xs) &*& pxs <= (int *)UINTPTR_MAX;
    ensures [f]ints(pxs, N, xs) &*& pxs + N <= (int *)UINTPTR_MAX &*& forall(xs, int_within_limits) == true;

lemma void ints_split(int *arr, int offset);
    requires [?f]ints(arr, ?N, ?as) &*& 0 <= offset &*& offset <= N;
    ensures [f]ints(arr, offset, take(offset, as)) &*& [f]ints(arr + offset, N - offset, drop(offset, as));

lemma void ints_join(int *a);
    requires [?f]ints(a, ?M, ?as) &*& [f]ints(a + M, ?N, ?bs);
    ensures [f]ints(a, M + N, append(as, bs));

lemma void ints_unseparate_same(int *arr, list<int> xs);
    requires ints(arr, ?M, take(M, xs)) &*& integer(arr + M, head(drop(M, xs))) &*& ints(arr + M + 1, ?N, tail(drop(M, xs))) &*& length(xs) == M + N + 1;
    ensures ints(arr, M + N + 1, xs) &*& head(drop(M, xs)) == nth(M, xs);

lemma void ints_unseparate(int *arr, int i, list<int> xs);
    requires ints(arr, i, take(i, xs)) &*& integer(arr + i, ?y) &*& ints(arr + i + 1, length(xs) - i - 1, tail(drop(i, xs)));
    ensures ints(arr, length(xs), update(i, y, xs));

#endif
